plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.17.2'
}

group 'org.yayarh'
version '1.2'

sourceCompatibility = '11'
targetCompatibility = '11'
mainClassName = "$moduleName/org.yayarh.Launcher"

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation "com.google.zxing:core:3.3.0"
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.0.0'
}

application {
    mainClassName
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jlink {
    launcher {
        name = 'TextToQR'
        jvmArgs = applicationDefaultJvmArgs as List<String>
    }
    jpackage {
        installerOptions = [
                '--description', 'QRCode generator ._.',
                '--copyright', 'All copyrights go to the association of Secret Batata Ninjas',
                '--vendor', 'The Potato Ninja',
        ]

        installerType = project.findProperty('installerType')
        // we will pass this from the command line (example: -PinstallerType=msi)

        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/org.yayarh/logoIcon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/pdfdecorator/gui/icon.icns']
        }
    }
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
//    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

javafx {
    version = '11.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}